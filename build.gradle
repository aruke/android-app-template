buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

/**
 * Common configuration for sub-projects. Includes version config, compiler options &
 * dependencies common to all modules.
 */
subprojects {
    def isApp = project.name == "app"
    apply plugin: isApp ? 'com.android.application' : 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 29

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 29
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    dependencies {
        // Kotlin Standard Lib for Java 8
        implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.72'

        // KotlinX
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.6'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.3'

        // Koin for Kotlin
        implementation 'org.koin:koin-core:2.1.0'

        // Koin for Unit tests
        testImplementation 'org.koin:koin-test:2.1.0'

        // Timber
        implementation 'com.jakewharton.timber:timber:4.7.1'

        // Tests
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}